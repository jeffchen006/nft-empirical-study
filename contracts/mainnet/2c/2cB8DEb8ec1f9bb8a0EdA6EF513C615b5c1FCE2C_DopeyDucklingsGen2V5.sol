//SPDX-License-Identifier: UNLICENSED

// okay#1746
// ▁ ▂ ▄ ▅ 𝔇𝔦𝔞𝔷𝔈𝔱𝔬 ▅ ▄ ▂ ▁#6666
/*          
                                            ██████████                                  
                                      ░░  ██░░░░░░░░░░██                                
                                        ██░░░░░░░░░░░░░░██                              
                                        ██░░░░░░░░████░░██████████                      
                            ██          ██░░░░░░░░████░░██▒▒▒▒▒▒██                      
                          ██░░██        ██░░░░░░░░░░░░░░██▒▒▒▒▒▒██                      
                          ██░░░░██      ██░░░░░░░░░░░░░░████████                        
                        ██░░░░░░░░██      ██░░░░░░░░░░░░██                              
                        ██░░░░░░░░████████████░░░░░░░░██                                
                        ██░░░░░░░░██░░░░░░░░░░░░░░░░░░░░██                              
                        ██░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░██                            
                        ██░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░██                            
                        ██░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░██                            
                        ██░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░██                            
                        ██░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░██                            
                        ██░░░░░░░░░░░░░░░░░░░░░░░░░░░░░░██                              
                          ██░░░░░░░░░░░░░░░░░░░░░░░░░░██                                
                            ██████░░░░░░░░░░░░░░░░████                                  
                                  ████████████████                                      
*/
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC721/extensions/IERC721EnumerableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/ERC721Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/IERC721Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/security/PausableUpgradeable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/IERC721Enumerable.sol";
import "@openzeppelin/contracts-upgradeable/utils/CountersUpgradeable.sol";


import "./iquack.sol";
import "./iduckhouse.sol";
import "./iRandom.sol";

contract DopeyDucklingsGen2V5 is
    ERC721Upgradeable,
    OwnableUpgradeable,
    PausableUpgradeable
{
    using CountersUpgradeable for CountersUpgradeable.Counter;
    // using SafeMathUpgradeable for uint256;
    mapping(uint256=>uint256) tokenLastWrites;
    mapping(address=>uint256) addressLocks;
    mapping(address=>bool) admins;

    IERC721Enumerable dd;
    IQuack quack;
    IDuckHouse dh;
    IRandom r;

    mapping(address=>uint256) _hunterBalances;

    bool public quackSale;
    uint256 MAX_HUNTERS;
    uint256 HUNTER_COST_Q;

    uint256 MAX_DUCKS_MINT_PUBLIC_SALE;
    uint256 MAX_DUCKS_MINT_PUBLIC_SALE_PER_TX;
    uint256 DUCKS_MINT_COST_E;

    uint256 MAX_DUCKS_MINT_GAME;
    uint256 MAX_KING_DUCKS;

    uint256[] internal UNSTAKED_DUCKS;      //used for tracking killable ducks
    uint256[] internal UNSTAKED_HUNTERS;    //used for tracking killable hunters

    mapping(address=>uint256[]) public ownerTokens; 

    event KingDuckMinted(address indexed _from, uint256 _id);
    event DuckStolen(address indexed _from, address indexed _to, uint256 _id);
    event DuckKilled(address indexed _from, address indexed _to, uint256 _id);
    event HunterKilled(address indexed _from, address indexed _to, uint256 _id);
    event CeaseFire();

    CountersUpgradeable.Counter private _hunterIdCounter;
    CountersUpgradeable.Counter private _duckPublicCounter;
    CountersUpgradeable.Counter private _duckAdminCounter;
    CountersUpgradeable.Counter private _duckKingCounter;

    bool public stealEnabled;
    bool public killEnabled;
    uint256 public killInterval;
    uint256 public killQuantity;
    uint256 public lastKill;

    CountersUpgradeable.Counter private _mintActionCounter;
    CountersUpgradeable.Counter private _huntersKilled;
    CountersUpgradeable.Counter private _huntersStaked;
    CountersUpgradeable.Counter private _ducksStaked;
    CountersUpgradeable.Counter private _ducksKilled;
    CountersUpgradeable.Counter private _ducksStolen;
    uint256[] internal STAKED_KINGS;         // used for tracking stealers

    bool private ceaseFire;
    uint256 private version;
    string private baseuri;
    constructor() {}

    function initialize() public initializer {
        __ERC721_init("Dopey Ducklings Gen 2", "DD2");
        __Ownable_init();
        __Pausable_init();
        dd = IERC721Enumerable(0x4C1E4718818Ac8327EDC2552883ac9F19a5d0eAa);
        quack = IQuack(0x328dA827813881Ac40De42923AE734ba502Ad1A0);
        dh = IDuckHouse(0xA12136FA993C54e244760a5008f80ac8b39AA8A9);

        admins[address(dh)] = true;
        admins[owner()] = true;

        MAX_HUNTERS = 2000;
        HUNTER_COST_Q = 5 ether;

        MAX_DUCKS_MINT_PUBLIC_SALE = 3000;
        MAX_DUCKS_MINT_PUBLIC_SALE_PER_TX = 5;
        DUCKS_MINT_COST_E = 0.05 ether;

        MAX_DUCKS_MINT_GAME = 45000;

        MAX_KING_DUCKS = 5000;

        killEnabled = true;
        killInterval = 60*60*24;
        killQuantity = 1;
        lastKill = block.timestamp;

        _pause();
    }

    function upgradeFromV4() public {
        require(version == 4);
        version = 5;
        DUCKS_MINT_COST_E = 0 ether;
    }

    function setRandom(address _random) public onlyOwner {
        r = IRandom(_random);
    }

    modifier humansOnly() {
        uint256 size;
        address account = _msgSender();
        assembly {
            size := extcodesize(account)
        }
        require(admins[_msgSender()] || size == 0, "Humans only");
        _;
    }

    modifier onlyFromDuckHouse() {
        require(admins[_msgSender()] || _msgSender() == address(dh) || _msgSender() == owner());
        _;
    }

    function getDucksMinted() view public humansOnly returns(uint256){
        return _duckPublicCounter.current();
    }

    function getEthDucksMinted() view public humansOnly returns(uint256){
        return _duckPublicCounter.current() - _duckAdminCounter.current();
    }

    function getHuntersMinted() view public humansOnly returns (uint256) {
        return _hunterIdCounter.current();
    }

    function getDucksStolen() view public humansOnly returns (uint256) {
        return _ducksStolen.current();
    }

    function getHuntersKilled() view public humansOnly returns (uint256) {
        return _huntersKilled.current();
    }

    function getDucksKilled() view public humansOnly returns(uint256) {
        return _ducksKilled.current();
    }

    function getDucksStaked() view public humansOnly returns(uint256) {
        return _ducksStaked.current();
    }

    function getHuntersStaked() view public humansOnly returns(uint256) {
        return _huntersStaked.current();
    }

    function userCanMintHunter(address user) view public returns(bool) {
        return (dd.balanceOf(user) + dh.stakedDuckCountByOwner(user)) > 0;
    }

    function getHuntersMintedForUser(address user) view public humansOnly returns(uint256) {
        return _hunterBalances[user];
    }

    function mintHunter(uint256 numToMint, bool stake) public humansOnly whenNotPaused {
        require(_hunterBalances[_msgSender()] + numToMint <= 5, "Maximum 5 hunters per wallet");
        // get balance of user in DD / staking wallet
        uint256 balance = dd.balanceOf(_msgSender()) + dh.stakedDuckCountByOwner(_msgSender());
        require(balance > 0, "Not a genesis holder");
        
        // spend $quack
        uint256 totalCost = HUNTER_COST_Q * numToMint;
        quack.burnFrom(_msgSender(), totalCost);

        // mint hunter
        for (uint256 i = 0; i < numToMint; i++) {
            _mintHunter(stake);
        }
        _hunterBalances[_msgSender()] += numToMint;
    }

    function _mintHunter(bool stake) private returns(uint256) {
        _hunterIdCounter.increment();
        uint256 id = _hunterIdCounter.current();
        require(id <= MAX_HUNTERS, "Max hunters minted");
        if (stake) {
            _mint(address(dh), id);
            dh._setStakeForGen2Token(id, _msgSender());
        } else {
            _mint(_msgSender(), id);
        }
        return id;
    }

    function _u_get_a_king_duck(uint256 tokenId) private returns (bool){
        _mintActionCounter.increment();
        bool result = r.r(keccak256(abi.encodePacked(tokenId, _msgSender(), block.timestamp, _mintActionCounter.current())),true, 50000) < 5000;
        return result;
    }

    function adminMintDuck(uint256 numToMint) public onlyOwner {
        for (uint256 i; i < numToMint; i++) {
            _duckPublicCounter.increment();
            _duckAdminCounter.increment();
            require(_duckPublicCounter.current() <= MAX_DUCKS_MINT_GAME, "Maximum supply reached");
            _mint(_msgSender(), _duckPublicCounter.current() + MAX_HUNTERS);
        }
    }

    function adminMintKing(uint256 numToMint) public onlyOwner {
        for (uint256 i; i < numToMint; i++) {
            _duckPublicCounter.increment();
            _duckAdminCounter.increment();
            _duckKingCounter.increment();
            require(_duckPublicCounter.current() <= MAX_DUCKS_MINT_GAME, "Maximum supply reached");
            _mint(_msgSender(), MAX_HUNTERS + MAX_DUCKS_MINT_PUBLIC_SALE + MAX_DUCKS_MINT_GAME - MAX_KING_DUCKS + _duckKingCounter.current());
        }
    }

    function adminMintHunter(uint256 numToMint) public onlyOwner {
        for (uint i; i < numToMint; i++) {
            _mintHunter(false);
        }
    }

    function mintDuckComposite(uint256 numToMint, bool stake) public humansOnly whenNotPaused payable {
        bool isEthSale = true;
        if (((_duckPublicCounter.current() - _duckAdminCounter.current()) < MAX_DUCKS_MINT_PUBLIC_SALE) && !quackSale) {
            // requirements for eth sale
            require(numToMint > 0 && numToMint <= MAX_DUCKS_MINT_PUBLIC_SALE_PER_TX, "Invalid mint number");
            require(msg.value >= DUCKS_MINT_COST_E * numToMint, "Incorrect value sent");
            require(numToMint + (_duckPublicCounter.current()-_duckAdminCounter.current()) <= MAX_DUCKS_MINT_PUBLIC_SALE, "Not enough public ducks remaining to mint");
        } else {
            // requirements for quack sale
            require(numToMint + _duckPublicCounter.current() <= (MAX_DUCKS_MINT_GAME+MAX_DUCKS_MINT_PUBLIC_SALE), "Minting would exceed supply");
            require(msg.value == 0, "$QUACK ONLY");
            isEthSale = false;
            // do quack burning here
        }
        for (uint i; i < numToMint; i++) {
            uint256 mintId = _mintDuckComposite(isEthSale);
            if (_stealFlow(mintId)) continue;

            if (stake) {
                _transfer(_msgSender(), address(dh), mintId);
                dh._setStakeForGen2Token(mintId, _msgSender());
            }
        }

        // post mint autokill action
        _autokill();
    }

    function _mintDuckComposite(bool isEthSale) private returns(uint256){
        uint256 id;
        bool kingFlow;
        if (_duckKingCounter.current() < MAX_KING_DUCKS && _u_get_a_king_duck(_duckKingCounter.current())) {
            // get duck king id inlined
            kingFlow = true;
            _duckKingCounter.increment();
            id = MAX_HUNTERS + MAX_DUCKS_MINT_PUBLIC_SALE + MAX_DUCKS_MINT_GAME - MAX_KING_DUCKS + _duckKingCounter.current();
        } else {
            _duckPublicCounter.increment();
            id = _duckPublicCounter.current() + MAX_HUNTERS;
            require(id <= MAX_DUCKS_MINT_GAME + MAX_DUCKS_MINT_PUBLIC_SALE + MAX_HUNTERS, "Mint limit reached");
        }
        if (id > (MAX_HUNTERS + MAX_DUCKS_MINT_PUBLIC_SALE + MAX_DUCKS_MINT_GAME - MAX_KING_DUCKS)) {
            // we cap _duckPublicCounter() at 45k and instead switch to the king id selection if this occurs
            if (!kingFlow) {
                _duckPublicCounter.decrement();
                _duckKingCounter.increment();
                id = MAX_HUNTERS + MAX_DUCKS_MINT_PUBLIC_SALE + MAX_DUCKS_MINT_GAME - MAX_KING_DUCKS + _duckKingCounter.current();
            }
            emit KingDuckMinted(_msgSender(), id);
        }

        // if $QUACK sale, burn token cost
        if (!isEthSale) {
            uint256 mc = getQuackMintCost(_duckPublicCounter.current() + _duckKingCounter.current());
            quack.burnFrom(_msgSender(), mc);
        }
        _mint(_msgSender(), id);
        return id;
    }

    function getQuackMintCostForNTokens(uint256 n) view public humansOnly returns (uint256 cost) {
        uint256 start = _duckPublicCounter.current() + 1;
        for (uint256 i; i < n; i++) {
            cost += getQuackMintCost(start + i);
        }
        return cost;
    }

    function getQuackMintCost(uint256 id) pure public returns(uint256) {
        if (id < 10000) return 1 ether;
        else if (id < 20000) return 2 ether;
        else if (id < 30000) return 4 ether;
        else if (id < 40000) return 8 ether;
        else if (id < 50000) return 16 ether;
        else return 999 ether;
    }

    function _duckHouseStakingCallback() public onlyFromDuckHouse {
        _autokill();
    }

    // remove token, isDuck for removing ducks or hunters;
    function _removeUnstakedTokenAtPosition(uint256 position, bool isDuck, bool isKing) internal {
        uint256[] storage UNSTAKED = isDuck ? (isKing ? STAKED_KINGS : UNSTAKED_DUCKS) : UNSTAKED_HUNTERS;
        delete UNSTAKED[position];
        UNSTAKED[position] = UNSTAKED[
            UNSTAKED.length-1
        ];
        UNSTAKED.pop();
    }

    /** finds a tokenId in the unstaked ducks/hunters or staked kings array. returns whether it was found + position
    */
    function _findUnstakedTokenPosition(uint256 tokenId, bool isDuck, bool isKing) internal view returns(bool found, uint256 position){
        uint256[] storage UNSTAKED = isDuck ? (isKing ? STAKED_KINGS : UNSTAKED_DUCKS) : UNSTAKED_HUNTERS;
        for (uint256 i = 0; i < UNSTAKED.length; i++) {
            if (UNSTAKED[i] == tokenId) {
                position = i;
                found = true;
                break;
            }
        }
        return (found, position);
    }    

    // === stealing mechanic ===
    // duck steal flow; 
    // ret true for stolen, false otherwise
    function _stealFlow(uint256 id) private returns(bool) {
        if (!stealEnabled) return false;
        (bool didSteal, address killer) = _maybeSteal(id);
        if (didSteal) {
            _killRandomHunter(killer);
            return true;
        }

        return false;
    }

    // stealing mechanic
    function _maybeSteal(uint256 id) private returns(bool, address){
        bool doSteal = r.r(keccak256(abi.encodePacked(id, _msgSender(), block.timestamp)), true, 10) == 0;
        if (doSteal) {
            // if no king ducks return
            if (_duckKingCounter.current() == 0 || STAKED_KINGS.length == 0) return (false, address(0));

            // get a king duck
            uint256 kId = r.r2(keccak256(abi.encodePacked(id, _msgSender(), block.number)), false, STAKED_KINGS.length);
            kId = STAKED_KINGS[kId];
            address kIdOwner = dh.getGen2StakeStatus(kId).user;
            if (_msgSender() == kIdOwner) {
                // If the minter also owns the king who steals, return to save gas + skip transfers;
                return (false, address(0));
            }

            // transfer to king duck
            _ducksStolen.increment();
            _transfer(_msgSender(), kIdOwner, id);
            emit DuckStolen(_msgSender(), kIdOwner, id);
            return (true, ownerOf(kId));
        }
        return (false, address(0));
    }
    // === end stealing mechanic ===

    // === killing mechanic ===
    function _getKillableToken(bool isDuck) view private returns (uint256 tokenId, uint256 position) {
        uint256[] memory UNSTAKED = isDuck ? UNSTAKED_DUCKS : UNSTAKED_HUNTERS;
        position = r.r(keccak256(abi.encodePacked(block.number, block.timestamp, _msgSender())), true, UNSTAKED.length);
        tokenId = UNSTAKED[position];
        return (tokenId, position);
    }

    /**
    _removeOwnerToken removes a token from a list of tokens owned by that owner
    */
    function _removeOwnerToken(address _owner, uint256 target) internal {
        if (_owner == address(0)) return;

        uint256 position = 0;
        bool found = false;
        for (uint256 i; i < ownerTokens[_owner].length; i++) {
            if (ownerTokens[_owner][i] == target) {
                found = true;
                position = i;
                break;
            }
        }
        if (found) {
            delete ownerTokens[_owner][position];
            ownerTokens[_owner][position] = ownerTokens[_owner][ownerTokens[_owner].length-1];
            ownerTokens[_owner].pop();
        }
    }

    /**
    _killDuck 
    */
    function _killDuck(uint256 id) private {
        _ducksKilled.increment();
        address _owner = ownerOf(id);
        emit DuckKilled(_owner, _owner, id);
        _burn(id);
    }

    function _autokill() private {
        if (killEnabled && (lastKill + killInterval) < block.timestamp) {
            for (uint256 i; i < killQuantity; i++) {
                _killRandomDuck();
            }
            lastKill = block.timestamp;
        }
    }

    /** 
    killDuck()
    */
    function _killRandomDuck() private {
        uint256 duckId;
        uint256 position;
        if (UNSTAKED_DUCKS.length == 0) return;
        (duckId, position) = _getKillableToken(true);
        _killDuck(duckId);
        dh.killCallback();
    }

    /**
    _killHunter burns a token and emits a hunterkilled event
    */
    function _killHunter(uint256 id, address killer) private {
        _huntersKilled.increment();
        emit HunterKilled(killer, ownerOf(id), id);
        _burn(id);
    }

    /** 
    _killRandomHunter selects an unstaked hunter and burns the token
    */
    function _killRandomHunter(address killer) private {
        uint256 hunterId;
        uint256 position;
        if (UNSTAKED_HUNTERS.length == 0) {
            return;
        } // guard against nothing to kill
        (hunterId, position) = _getKillableToken(false);
        _killHunter(hunterId, killer);
    }
    // === end killing mechanic ===

    // === enumeration things ===
    // get unstaked ducks and hunters
    function walletOfOwner(address _owner) public view humansOnly returns (uint256[] memory) {
        return ownerTokens[_owner];
    }

    function balanceOf(address _owner) override public view humansOnly returns(uint256){
        return super.balanceOf(_owner);
    }
    // === end enumeration things ===

    // === admin things ===
    function togglePaused() onlyOwner public{
        if (paused()) {
            _unpause();
        } else {
            _pause();
        }
    }

    function callCeaseFire() public onlyOwner {
        _pause();
        killEnabled = false;
        emit CeaseFire();
    }

    function toggleQuackSale() public onlyOwner {
        quackSale = !quackSale;
    }

    function toggleStealEnabled() public onlyOwner {
        stealEnabled = !stealEnabled;
    }

    function toggleKillEnabled() public onlyOwner {
        killEnabled = !killEnabled;
    }

    function setKillInterval(uint256 _interval) public onlyOwner {
        killInterval = _interval;
    }

    function setKillQuantity(uint256 _quantity) public onlyOwner {
        killQuantity = _quantity;
    }

    function withdraw() public onlyOwner {
        uint256 split = (address(this).balance/1000) * 475;
        payable(0xD59F530a4C970982097Cf2CDf99e94BfD68CCA12).transfer(split);
        payable(0x3E4889cee5b4927BB3209fE43A92b42F32F647Bb).transfer(split);
        payable(0x9c43Ae878298D9Dc994f59CE13C3cF9738aeC0b6).transfer(address(this).balance);
    }

    function _baseURI() internal view override returns(string memory) {
        return baseuri;
    }

    function setBaseURI(string calldata _base) public onlyOwner {
        baseuri = _base;
    }

    function setMaxDucksMintPublicSale(uint256 n) public onlyOwner {
        MAX_DUCKS_MINT_PUBLIC_SALE = n;
    }

    function setMaxDucksMintGame(uint256 n) public onlyOwner {
        MAX_DUCKS_MINT_GAME = n;
    }

    function setMaxKings(uint256 n) public onlyOwner {
        MAX_KING_DUCKS = n;
    }

    function modPublicDuckCounter(bool increase) public onlyOwner {
        increase ? _duckPublicCounter.increment() : _duckPublicCounter.decrement();
    }
    function modAdminDuckCounter(bool increase) public onlyOwner {
        increase ? _duckAdminCounter.increment() : _duckAdminCounter.decrement();
    }
    function modHunterCounter(bool increase) public onlyOwner {
        increase ? _hunterIdCounter.increment() : _hunterIdCounter.decrement();
    }
    function modKingDuckCounter(bool increase) public onlyOwner {
        increase ? _duckKingCounter.increment() : _duckKingCounter.decrement();
    }

    function adminMintTokenN(uint256 id) public onlyOwner {
        _mint(_msgSender(), id);
    }

    // === end admin things ===
    
    function _mint(address to, uint256 tokenId) internal virtual override {
        _tokenIsChanging(tokenId);
        _lockAddress(_msgSender());
        super._mint(to, tokenId);
    }

    /**
    transferFrom overridden to skip manual approval by users (and gas)
    */
    function transferFrom(address from, address to, uint256 tokenId) public virtual override addressBlocked(from) notWhileTokenChanging (tokenId){
        require(_msgSender() == address(dh) || _isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _transfer(from, to, tokenId);
    }

    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual override returns (bool) {
        require(_exists(tokenId), "ERC721: transfer caller is not owner nor approved");
        address owner = ERC721Upgradeable.ownerOf(tokenId);
        return (spender == owner || getApproved(tokenId) == spender || isApprovedForAll(owner, spender));
    }

    /**
    _beforeTokenTransfer hooks all transfers to keep track of unstaked tokens as well as users' unstaked tokens
    This hook is called on
    - burn
    - mint
    - transfer
    */
    function _beforeTokenTransfer(address from, address to, uint256 tokenId) internal virtual override {
        bool isDuck = tokenId > MAX_HUNTERS ? true : false;
        bool isKing = tokenId > MAX_DUCKS_MINT_GAME; // if the token is king there is no need to add it to unstaked lists
        // owner balances
        _removeOwnerToken(from, tokenId);
        if (to != address(dh)) {
            // we only add to non dh balances because who cares about what dh holds
            ownerTokens[to].push(tokenId);
        }

        // Stake/unstake things
        if (from == address(dh)) {
            // If not king, add token to unstaked arrays
            if (!isKing) {
                uint256[] storage UNSTAKED = isDuck ? UNSTAKED_DUCKS : UNSTAKED_HUNTERS;
                UNSTAKED.push(tokenId);
                isDuck ? _ducksStaked.decrement() : _huntersStaked.decrement();
            } else {
            // if King, remove from staked kings
                (bool found, uint256 position) = _findUnstakedTokenPosition(tokenId, isDuck, isKing);
                if (found) {
                    _removeUnstakedTokenAtPosition(position, isDuck, isKing);
                }
                _ducksStaked.decrement();
            }

        } else if (to == address(dh)) {
            if(from == address(0)) return;
            // remove token from unstaked ducks/hunters
            if (!isKing) {
                isDuck ? _ducksStaked.increment() : _huntersStaked.increment();
                (bool found, uint256 position) = _findUnstakedTokenPosition(tokenId, isDuck, isKing);
                if (found) {
                    _removeUnstakedTokenAtPosition(position, isDuck, isKing);
                }
            } else {
                // add token to staked kings
                STAKED_KINGS.push(tokenId);
                _ducksStaked.increment();
            }
        }

        // mint
        if (from == address(0)) {
            if (!isKing) {
                uint256[] storage UNSTAKED = isDuck ? UNSTAKED_DUCKS : UNSTAKED_HUNTERS;
                UNSTAKED.push(tokenId);
            }
        }

        // burn
        if (to == address(0)) {
            // remove from unstaked
            if (!isKing) {
                (bool found, uint256 position) = _findUnstakedTokenPosition(tokenId, isDuck, isKing);
                if (found) {
                    _removeUnstakedTokenAtPosition(position, isDuck, isKing);
                }
            }
        }
    }

    /** Things I wish I didn't have to do */
    function _tokenIsChanging(uint256 tokenId) internal {
        tokenLastWrites[tokenId] = block.timestamp;
    }

    function _lockAddress(address toLock) internal {
        addressLocks[toLock] = block.timestamp;
    }

    /** checkLockedAddress
    * this is a callback for additional contracts to check whether a specified address should be
    * blocked from performing an action in this block
    */
    function checkLockedAddress(address addressToCheck) view public returns(bool){
        return addressLocks[addressToCheck] == block.timestamp;
    }

    modifier notWhileTokenChanging(uint256 tokenId) {
        require(
            admins[_msgSender()] || 
            (
                tokenLastWrites[tokenId] < block.timestamp &&
                tokenLastWrites[tokenId] != 0
            ), "noooo!");
        _;
    }

    // If an address mints, they are banned from performing this action in the same block
    modifier addressBlocked(address from) {
        require(admins[_msgSender()] || addressLocks[from] < block.timestamp, "noooo!");
        _;
    }

    function ownerOf(uint256 tokenId) public view virtual override humansOnly returns (address) {
        return super.ownerOf(tokenId);
    }

    function tokenURI(uint256 tokenId) public view virtual override humansOnly returns (string memory) {
        return string(abi.encodePacked(super.tokenURI(tokenId),".json"));
    }

    function approve(address to, uint256 tokenId) public virtual override humansOnly notWhileTokenChanging(tokenId) {
        return super.approve(to, tokenId);
    }

    function getApproved(uint256 tokenId) public view virtual override humansOnly notWhileTokenChanging(tokenId) returns (address) {
        return super.getApproved(tokenId);
    }

    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override notWhileTokenChanging(tokenId) addressBlocked(from) {
        return super.safeTransferFrom(from, to, tokenId);
    }

    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) public virtual override notWhileTokenChanging(tokenId) addressBlocked(from) {
        return super.safeTransferFrom(from, to, tokenId, _data);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Enumerable.sol)

pragma solidity ^0.8.0;

import "../IERC721Upgradeable.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721EnumerableUpgradeable is IERC721Upgradeable {
    /**
     * @dev Returns the total amount of tokens stored by the contract.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.
     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256 tokenId);

    /**
     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.
     * Use along with {totalSupply} to enumerate all tokens.
     */
    function tokenByIndex(uint256 index) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/ERC721.sol)

pragma solidity ^0.8.0;

import "./IERC721Upgradeable.sol";
import "./IERC721ReceiverUpgradeable.sol";
import "./extensions/IERC721MetadataUpgradeable.sol";
import "../../utils/AddressUpgradeable.sol";
import "../../utils/ContextUpgradeable.sol";
import "../../utils/StringsUpgradeable.sol";
import "../../utils/introspection/ERC165Upgradeable.sol";
import "../../proxy/utils/Initializable.sol";

/**
 * @dev Implementation of https://eips.ethereum.org/EIPS/eip-721[ERC721] Non-Fungible Token Standard, including
 * the Metadata extension, but not including the Enumerable extension, which is available separately as
 * {ERC721Enumerable}.
 */
contract ERC721Upgradeable is Initializable, ContextUpgradeable, ERC165Upgradeable, IERC721Upgradeable, IERC721MetadataUpgradeable {
    using AddressUpgradeable for address;
    using StringsUpgradeable for uint256;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to owner address
    mapping(uint256 => address) private _owners;

    // Mapping owner address to token count
    mapping(address => uint256) private _balances;

    // Mapping from token ID to approved address
    mapping(uint256 => address) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping(address => mapping(address => bool)) private _operatorApprovals;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    function __ERC721_init(string memory name_, string memory symbol_) internal onlyInitializing {
        __Context_init_unchained();
        __ERC165_init_unchained();
        __ERC721_init_unchained(name_, symbol_);
    }

    function __ERC721_init_unchained(string memory name_, string memory symbol_) internal onlyInitializing {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165Upgradeable, IERC165Upgradeable) returns (bool) {
        return
            interfaceId == type(IERC721Upgradeable).interfaceId ||
            interfaceId == type(IERC721MetadataUpgradeable).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _balances[owner];
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: owner query for nonexistent token");
        return owner;
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }

    /**
     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty
     * by default, can be overriden in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721Upgradeable.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(
            _msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not owner nor approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _safeTransfer(from, to, tokenId, _data);
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * `_data` is additional data, it has no specified format and it is sent in call to `to`.
     *
     * This internal function is equivalent to {safeTransferFrom}, and can be used to e.g.
     * implement alternative mechanisms to perform token transfer, such as signature-based.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeTransfer(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted (`_mint`),
     * and stop existing when they are burned (`_burn`).
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _owners[tokenId] != address(0);
    }

    /**
     * @dev Returns whether `spender` is allowed to manage `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        require(_exists(tokenId), "ERC721: operator query for nonexistent token");
        address owner = ERC721Upgradeable.ownerOf(tokenId);
        return (spender == owner || getApproved(tokenId) == spender || isApprovedForAll(owner, spender));
    }

    /**
     * @dev Safely mints `tokenId` and transfers it to `to`.
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is
     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.
     */
    function _safeMint(
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _mint(to, tokenId);
        require(
            _checkOnERC721Received(address(0), to, tokenId, _data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }

    /**
     * @dev Mints `tokenId` and transfers it to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - `to` cannot be the zero address.
     *
     * Emits a {Transfer} event.
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721Upgradeable.ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId);

        // Clear approvals
        _approve(address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);
    }

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {Transfer} event.
     */
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(ERC721Upgradeable.ownerOf(tokenId) == from, "ERC721: transfer of token that is not own");
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // Clear approvals from the previous owner
        _approve(address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);
    }

    /**
     * @dev Approve `to` to operate on `tokenId`
     *
     * Emits a {Approval} event.
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721Upgradeable.ownerOf(tokenId), to, tokenId);
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits a {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC721: approve to caller");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param _data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) private returns (bool) {
        if (to.isContract()) {
            try IERC721ReceiverUpgradeable(to).onERC721Received(_msgSender(), from, tokenId, _data) returns (bytes4 retval) {
                return retval == IERC721ReceiverUpgradeable.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}
    uint256[44] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165Upgradeable.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721Upgradeable is IERC165Upgradeable {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (proxy/utils/Initializable.sol)

pragma solidity ^0.8.0;

import "../../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To initialize the implementation contract, you can either invoke the
 * initializer manually, or you can include a constructor to automatically mark it as initialized when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() initializer {}
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        // If the contract is initializing we ignore whether _initialized is set in order to support multiple
        // inheritance patterns, but we only do this in the context of a constructor, because in other contexts the
        // contract may have been reentered.
        require(_initializing ? _isConstructor() : !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} modifier, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    function _isConstructor() private view returns (bool) {
        return !AddressUpgradeable.isContract(address(this));
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal onlyInitializing {
        __Context_init_unchained();
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal onlyInitializing {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract PausableUpgradeable is Initializable, ContextUpgradeable {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    function __Pausable_init() internal onlyInitializing {
        __Context_init_unchained();
        __Pausable_init_unchained();
    }

    function __Pausable_init_unchained() internal onlyInitializing {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Enumerable.sol)

pragma solidity ^0.8.0;

import "../IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Enumerable is IERC721 {
    /**
     * @dev Returns the total amount of tokens stored by the contract.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.
     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256 tokenId);

    /**
     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.
     * Use along with {totalSupply} to enumerate all tokens.
     */
    function tokenByIndex(uint256 index) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Counters.sol)

pragma solidity ^0.8.0;

/**
 * @title Counters
 * @author Matt Condon (@shrugs)
 * @dev Provides counters that can only be incremented, decremented or reset. This can be used e.g. to track the number
 * of elements in a mapping, issuing ERC721 ids, or counting request ids.
 *
 * Include with `using Counters for Counters.Counter;`
 */
library CountersUpgradeable {
    struct Counter {
        // This variable should never be directly accessed by users of the library: interactions must be restricted to
        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add
        // this feature: see https://github.com/ethereum/solidity/issues/4637
        uint256 _value; // default: 0
    }

    function current(Counter storage counter) internal view returns (uint256) {
        return counter._value;
    }

    function increment(Counter storage counter) internal {
        unchecked {
            counter._value += 1;
        }
    }

    function decrement(Counter storage counter) internal {
        uint256 value = counter._value;
        require(value > 0, "Counter: decrement overflow");
        unchecked {
            counter._value = value - 1;
        }
    }

    function reset(Counter storage counter) internal {
        counter._value = 0;
    }
}

pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";

interface IQuack is IERC20Upgradeable {
    function burnFrom(address from, uint256 amount) external;
    function initialize(string memory, string memory) external;
    function mint(address, uint256) external;
    function grantRole(bytes32, address) external;
    // function balanceOf(address account) external;
}

pragma solidity ^0.8.0;

interface IDuckHouse {
    struct StakeStatus{
        bool staked;
        uint88 since;
        address user;
    }
    function killCallback() external;
    function _setStakeForGen2Token(uint256 id, address _owner) external;
    function stakedDuckCountByOwner(address _owner) view external returns(uint256);
    function getStakedDuckCountByOwner(address _owner) external returns(uint256);
    function getStakedDuckOfOwnerByIndex(address _owner, uint256 index) external returns(uint256);
    function getGen2StakeStatus(uint256 id) external returns(StakeStatus memory);
}

pragma solidity ^0.8.0;

interface IRandom {
    function r(bytes32, bool, uint256) view external returns(uint256);
    function r2(bytes32, bool, uint256) view external returns(uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165Upgradeable {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721Receiver.sol)

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721ReceiverUpgradeable {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721.onERC721Received.selector`.
     */
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC721Upgradeable.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721MetadataUpgradeable is IERC721Upgradeable {
    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Address.sol)

pragma solidity ^0.8.0;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;
import "../proxy/utils/Initializable.sol";

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal onlyInitializing {
        __Context_init_unchained();
    }

    function __Context_init_unchained() internal onlyInitializing {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library StringsUpgradeable {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165Upgradeable.sol";
import "../../proxy/utils/Initializable.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165Upgradeable is Initializable, IERC165Upgradeable {
    function __ERC165_init() internal onlyInitializing {
        __ERC165_init_unchained();
    }

    function __ERC165_init_unchained() internal onlyInitializing {
    }
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165Upgradeable).interfaceId;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20Upgradeable {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}